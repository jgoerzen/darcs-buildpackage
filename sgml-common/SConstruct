# vim: set filetype=python :
# arch-tag: general-purpose SCons build file for sgml-common

from glob import glob
import os

#Import('env')
d = env.Dictionary()
if not 'JADE' in d:
    d['JADE'] = 'jade'
if not 'INDEXNODE' in d:
    d['INDEXNODE'] = 'ch.index'
if not 'GTKIMGPATH' in d:
    d['GTKIMGPATH'] = '/usr/share/gtk-doc/data'

master = d['MASTERBASE'] 
mastersgml = master + '.sgml'
sources = [mastersgml] + glob('chapters/*.sgml') + glob('chapters/*/*.sgml')
db2htmlcmd = 'docbook-2-html -D $JADE ${HTMLARGS}  ${SOURCE}'
db2htmlindexcmd = 'docbook-2-html -D $JADE -O -V -O html-index ${HTMLARGS} ${SOURCE}'

#### PLAIN TEXT
Btxt = Builder(action="docbook2txt $SOURCE", src_suffix='.sgml', suffix='.txt')

#### PDF / POSTSCRIPT
Bpdf = Builder(action="docbook-2-pdf -D ${JADE} -q -O -V -O paper-size=Letter ${PDFARGS} ${SOURCE}",
        src_suffix='.sgml', suffix='.pdf')
Bps = Builder(action="pdftops ${SOURCE}", src_suffix='.pdf', suffix='.ps')

#HTML
Bhtml = Builder(action = [ \
        'if test -d ${TARGET.dir} ; then rm -r ${TARGET.dir} ; fi',
        'mkdir ${TARGET.dir}',
        db2htmlcmd,
        'mv ${MASTERBASE}-html/* ${TARGET.dir}/',
        'rmdir ${MASTERBASE}-html',
        'ln -s ${TOPNODE}.html ${TARGET.dir}/index.html',
        'cp ${GTKIMGPATH}/*.png ${TARGET.dir}/'])

# General setup

env.Append(BUILDERS = {'Text': Btxt, 'PS': Bps, 'PDF': Bpdf, 'HTML': Bhtml})

#### INDEX GENERATION
if 'DOINDEX' in d:
    Bindex = Builder(action = ['if test -d ${TARGET.dir}  ; then rm -r ${TARGET.dir} ; fi',
        "mkdir ${TARGET.dir}",
        "collateindex.pl -i $INDEXNODE -N -o $TARGET",
        db2htmlindexcmd,
        "mv ${MASTERBASE}-html/HTML.index ${TARGET.dir}/",
        "rm -r ${MASTERBASE}-html",
        "collateindex.pl -i $INDEXNODE -g -o $TARGET ${TARGET.dir}/HTML.index"])
    env['BUILDERS']['Index'] = Bindex
    index = env.Index('index/index.sgml', mastersgml)
    env.Depends(index, sources)
    env.Clean(index, 'index')
    deps = sources + [index]
else:
    deps = sources

##################################################
# BUILD RULES
##################################################
text = env.Text(mastersgml)
env.Depends(text, deps)

pdf = env.PDF(mastersgml)
env.Depends(pdf, deps)
env.Alias('pdf', pdf)
env.Clean(pdf, ['jadetex.cfg', '${MASTERBASE}.aux', '${MASTERBASE}.dvi',
        '${MASTERBASE}.jtex', '${MASTERBASE}.log', '${MASTERBASE}.out'])

ps = env.PS(pdf)
env.Alias('ps', ps)

html = env.HTML('html/index.html', mastersgml)
env.Depends(html, deps)
env.Alias('html', html)
env.Clean(html, 'html')

env.Default(html)

